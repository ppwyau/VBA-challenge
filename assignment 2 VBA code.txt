Sub StockAnalysis()
'declaring variables
    Dim ws As Worksheet
    Dim ticker As String
    Dim openPrice As Double
    Dim closePrice As Double
    Dim quarterlyChange As Double
    Dim percentChange As Double
    Dim totalVolume As Double
    Dim lastRow As Long
    Dim summaryRow As Long
    Dim greatestIncrease As Double
    Dim greatestDecrease As Double
    Dim greatestVolume As Double
    Dim greatestIncreaseTicker As String
    Dim greatestDecreaseTicker As String
    Dim greatestVolumeTicker As String

'loop through each worksheet, from Q1 to Q4 
    For Each ws In ThisWorkbook.Worksheets
'Check worksheet to ensure it contains Q (Quaterly) data
        If ws.Name Like "Q*" Then

            lastRow = ws.Cells(ws.Rows.Count, 1).End(xlUp).Row
            summaryRow = 2
            greatestIncrease = 0
            greatestDecrease = 0
            greatestVolume = 0

'loop each row, check ticker symbol and add up volume
            For i = 2 To lastRow
                If ws.Cells(i, 1).Value <> ws.Cells(i - 1, 1).Value Then
                    ticker = ws.Cells(i, 1).Value
                    openPrice = ws.Cells(i, 3).Value
                    totalVolume = ws.Cells(i, 7).Value
                Else
                    totalVolume = totalVolume + ws.Cells(i, 7).Value
                End If

'check ticker symbol change or if its the last row, then set close price calculate change and percentage change
                If ws.Cells(i + 1, 1).Value <> ticker Or i = lastRow Then
                    closePrice = ws.Cells(i, 6).Value
                    quarterlyChange = closePrice - openPrice
                    If openPrice <> 0 Then
                        percentChange = (quarterlyChange / openPrice) * 100
                    Else
                        percentChange = 0
                    End If

'add result to summary table
                    ws.Cells(summaryRow, 9).Value = ticker
                    ws.Cells(summaryRow, 10).Value = quarterlyChange
                    ws.Cells(summaryRow, 11).Value = percentChange
                    ws.Cells(summaryRow, 12).Value = totalVolume

                    If percentChange > greatestIncrease Then
                        greatestIncrease = percentChange
                        greatestIncreaseTicker = ticker
                    End If

                    If percentChange < greatestDecrease Then
                        greatestDecrease = percentChange
                        greatestDecreaseTicker = ticker
                    End If

                    If totalVolume > greatestVolume Then
                        greatestVolume = totalVolume
                        greatestVolumeTicker = ticker
                    End If

                    summaryRow = summaryRow + 1
                End If
            Next i

'add greatest increase, decrease, volume to summary
            ws.Cells(2, 14).Value = "Greatest % Increase"
            ws.Cells(2, 15).Value = greatestIncreaseTicker
            ws.Cells(2, 16).Value = greatestIncrease

            ws.Cells(3, 14).Value = "Greatest % Decrease"
            ws.Cells(3, 15).Value = greatestDecreaseTicker
            ws.Cells(3, 16).Value = greatestDecrease

            ws.Cells(4, 14).Value = "Greatest Total Volume"
            ws.Cells(4, 15).Value = greatestVolumeTicker
            ws.Cells(4, 16).Value = greatestVolume


        End If
    Next ws
End Sub





